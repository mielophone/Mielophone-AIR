<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx" 
				autoDrawBackground="false" 
				buttonMode="true" useHandCursor="true"
				width="100"
				creationComplete="itemrenderer1_creationCompleteHandler(event)"
				dataChange="itemrenderer1_dataChangeHandler(event)"
				click="itemrenderer1_clickHandler(event)">
	
	<s:layout>
		<s:VerticalLayout gap="5" horizontalAlign="center" verticalAlign="top" />
	</s:layout>
	
	<fx:Script>
		<![CDATA[
			import com.codezen.mse.models.Artist;
			import com.codezen.mse.services.LastFM;
			
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			import mx.utils.DisplayUtil;
			import mx.utils.ObjectProxy;
			
			[Bindable]
			[Embed(source="/assets/images/nocover.png")]
			private var nocoverImg:Class;
			
			// mask
			[Bindable]
			private var roundedMask:Sprite;
						
			private function itemrenderer1_creationCompleteHandler(event:FlexEvent):void
			{
				roundedMask = new Sprite();
				var cornerRadius:uint = 10;
				roundedMask.graphics.clear();
				roundedMask.graphics.beginFill(0xFF0000);
				roundedMask.graphics.drawRoundRect(0, 0, 90, 90, cornerRadius, cornerRadius);
				roundedMask.graphics.endFill();
			}
			
			private function itemrenderer1_dataChangeHandler(event:FlexEvent):void{
				image.source = nocoverImg;
				if(data == null) return;
				
				if( data.image == null || data.image.length < 5 ){
					var lfm:LastFM = new LastFM();
					lfm.addEventListener(Event.COMPLETE, onImage);
					lfm.addEventListener(ErrorEvent.ERROR, onLfmError);
					lfm.getArtistInfo(data as Artist);
				}else{
					preloadImage();
				}
			}
			
			private function onLfmError(e:Event):void{
				trace('aritst cover not found');
			}
			
			private function preloadImage():void{
				var imageLoader:Loader = new Loader();
				var theURL:String = data.image;
				var imageRequest:URLRequest = new URLRequest(theURL);
				
				imageLoader.contentLoaderInfo.addEventListener(Event.COMPLETE, onImageContent);
				imageLoader.contentLoaderInfo.addEventListener(IOErrorEvent.IO_ERROR, onError);
				imageLoader.load(imageRequest);
			}
			
			private function onImageContent(e:Event):void{
				var imageLoader:LoaderInfo = e.target as LoaderInfo;
				if( imageLoader.content == null ) return;
				if( imageLoader.content.width > 10 && imageLoader.content.height > 10 )
					image.source = imageLoader.content;
				
				imageLoader.removeEventListener(Event.COMPLETE, onImageContent);
				imageLoader.removeEventListener(IOErrorEvent.IO_ERROR, onError);
				imageLoader = null;
			}
			
			private function onError(e:Event):void{
				var imageLoader:LoaderInfo = e.target as LoaderInfo;
				imageLoader.removeEventListener(Event.COMPLETE, onImageContent);
				imageLoader.removeEventListener(IOErrorEvent.IO_ERROR, onError);
				imageLoader = null;
			}
			
			private function onImage(e:Event):void{
				var lfm:LastFM = e.target as LastFM;
				lfm.removeEventListener(Event.COMPLETE, onImage);
				
				data.image = lfm.artist.image;
				
				preloadImage();
				
				lfm = null;
			}
			
			private function itemrenderer1_clickHandler(event:MouseEvent):void
			{
				FlexGlobals.topLevelApplication.currentArtist = data as Artist;
				FlexGlobals.topLevelApplication.changeView(FlexGlobals.topLevelApplication.artistInfoView);
			}
			
		]]>
	</fx:Script>
	<!-- TODO: add image when there's no image available -->
	<s:BitmapImage width="90" height="90" source="{nocoverImg}" mask="{roundedMask}" maskType="clip"
				   smooth="true" smoothingQuality="high" id="image" scaleMode="letterbox" fillMode="clip" />
	<s:Label width="100%" text="{new ObjectProxy(data).name}" color="#FFFFFF" fontSize="16" textAlign="center" />
	
</s:ItemRenderer>
